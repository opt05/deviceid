apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt' //For Kotlin Annotation Processing with libraries (i.e. data binding)

android {
    compileSdkVersion 28
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "com.cwlarson.deviceid"
        minSdkVersion 14
        targetSdkVersion 28
        versionCode 10
        versionName "1.3.0"
    }
    signingConfigs {

        release {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file("release.properties")))
            storeFile rootProject.file(props.keyStore)
            storePassword props.keyStorePassword
            keyAlias props.keyAlias
            keyPassword props.keyAliasPassword
        }

    }
    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
            signingConfig signingConfigs.release
        }
    }
    dataBinding {
        enabled = true
    }
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
        javacOptions {
            // Increase the max count of errors from annotation processors. Default is 100.
            option("-Xmaxerrs", 500)
        }
    }
}

def androidx_version = '1.0.0'
def androidx_appcompat_version = '1.0.2'
def ktx_version = '1.0.1'
def lifecycle_version = '2.0.0'
def room_version = '2.0.0'
def paging_version = "2.1.0-rc01"
def coroutines_version = '1.1.0'
def leakcanary_version = "1.6.2"
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "androidx.core:core-ktx:$ktx_version"
    implementation "androidx.fragment:fragment-ktx:$androidx_version"
    implementation "androidx.appcompat:appcompat:$androidx_appcompat_version"
    implementation "androidx.recyclerview:recyclerview:$androidx_version"
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    implementation "androidx.viewpager:viewpager:$androidx_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:$androidx_version"
    implementation "com.google.android.material:material:$androidx_version"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"
    // ViewModel and LiveData
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"
    // Room
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // Kotlin coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    //Debug bridge for Android applications, enabling the powerful Chrome Developer Tools
    debugImplementation "com.facebook.stetho:stetho:1.5.0"
    //LeakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakcanary_version"
    debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leakcanary_version"
}
