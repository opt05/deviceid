apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt' //For Kotlin Annotation Processing with libraries (i.e. data binding)

android {
    compileSdkVersion 27
    dataBinding {
        enabled = true
    }

    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "com.cwlarson.deviceid"
        minSdkVersion 14
        targetSdkVersion 27
        versionCode 12
        versionName "1.3.2"
    }

    signingConfigs {

        release {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file("release.properties")))
            storeFile rootProject.file(props.keyStore)
            storePassword props.keyStorePassword
            keyAlias props.keyAlias
            keyPassword props.keyAliasPassword
        }

    }
    buildTypes {
        release {
            postprocessing {
                removeUnusedCode true
                removeUnusedResources true
                obfuscate true
                optimizeCode true
                proguardFile 'proguard-rules.pro'
            }
            signingConfig signingConfigs.release
        }
    }
}

ext.support_library_version = '27.1.1'
dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:appcompat-v7:$support_library_version"
    implementation "com.android.support:recyclerview-v7:$support_library_version"
    implementation "com.android.support:design:$support_library_version"
    implementation "com.android.support:cardview-v7:$support_library_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    //Data Binding support for Kotlin
    kapt "com.android.databinding:compiler:3.1.1"
    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:1.1.1"
    kapt "android.arch.lifecycle:compiler:1.1.1"
    // Room
    implementation "android.arch.persistence.room:runtime:1.0.0"
    kapt "android.arch.persistence.room:compiler:1.0.0"
    //Debug bridge for Android applications, enabling the powerful Chrome Developer Tools
    debugImplementation "com.facebook.stetho:stetho:1.5.0"
    //LeakCanary
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
}
